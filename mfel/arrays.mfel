@include("mfel/stdlib.mfel");
# defined in stdlib: amap, afold, asum, aprod, areduce, afilter

@println("******* testing arrays *******");

### array syntax:
### literals: [expr,expr,expr,...,expr]             (as in many other languages)
### get value at index i for array a: a(i)          (like a function call)
### set value at index i to v for array a: a(i,v)   (like a function call)
### creation: @array(size,init)
###   init can be
###     - a constant
###     - a 1 argument function to be called for each index
### length of array a: @len(a)
### + operator creates a new array from the concatenation of 2 arrays
### @append(a,expr): adds result of expr to the end of a

let a1 := [1,2,3,4];
@println("a1=",a1);

let a2 := @array(10,4);
@println("a2=",a2);

let a3 := @array(10, function(x) { 
    if x < 5 { 
        (x + 1) * 10 
    } else { 
        x + 1 
    } 
});
@println("a3=",a3);

let a4 := a1 + a2 + a3;
@println("a4=",a4);

@println("a1[2]=",a1(2));
let a1_2_3 := a1(2)+a1(3);
@println("a1[2]+a1[3]=",a1_2_3);
a1(2,a1_2_3); # a1[2]=a1_2_3
a1(0,100); # mutate a1 index 0
@println("a1=",a1); 

@println("map (*100) a1=",amap(function(x){x*100},a1));
@println("sum(a1)=",asum(a1));
@println("prod(a1)=",aprod(a1));
# test if all elements are greater than n (using fold)
let all_gt := dynamic(n,arr) { 
    afold(
        function(x,y){x and y},
        true,
        amap(closure(x){x > n},arr)
    )
};
# test if all elements are greater than n (using filter)
let all_gt := dynamic(n,arr) { 
    let filtered := afilter(closure(x){x > n},arr);
    @len(filtered) == @len(arr)
};
@println("all_gt_0(a1)=",all_gt(0,a1));
@println("all_gt_5(a1)=",all_gt(5,a1));
@println("chain_div([9,3,4])=",areduce(function(x,y){x*1.0/y},[9,3,4]));

@append(a1,42);
@println("a1=",a1); 
@println("filter >10 in a1=",afilter(function(x){x>10},a1));

@println("******************************");
