@println("******* loading stdlib *******");

### map function on arrays
var amap = function (f,a) {
    var i = 0;
    var out = @array(@len(a),());
    while i < @len(a) {
        out(i,f(a(i)));
        i := i + 1;
    }
    out
};

### fold function on arrays
var afold = function (f,acc,arr) {
    var i = 0;
    while i < @len(arr) {
        acc := f(acc,arr(i));
        i := i + 1;
    }
    acc
};

### sum of array elements
var asum = function(arr) {
    afold(function(x,y){x+y}, 0, arr)
};

### product of array elements
var aprod = function(arr) {
    afold(function(x,y){x*y}, 1, arr)
};

### reduce function on arrays
var areduce = function (f,arr) {
    if @len(arr) < 1 {
        ()
    } else {
        var acc = arr(0);
        var i = 1;
        while i < @len(arr) {
            acc := f(acc,arr(i));
            i := i + 1;
        }
        acc
    }
};

### filter function on arrays
var afilter = function (f,arr) {
    var i = 0;
    var out = @array(0,());
    while i < @len(arr) {
        if f(arr(i)) {
            @append(out,arr(i));
        }
        i := i + 1;
    }
    out
};

