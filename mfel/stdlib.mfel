@println("******* loading stdlib *******");

### map function on arrays
let amap := function (f,a) {
    let i := 0;
    let out := @array(@len(a),());
    while i < @len(a) {
        out(i,f(a(i)));
        i := i + 1;
    }
    out
};

### fold function on arrays
let afold := function (f,acc,arr) {
    let i := 0;
    while i < @len(arr) {
        acc := f(acc,arr(i));
        i := i + 1;
    }
    acc
};

### sum of array elements
let asum := context (afold) in closure(arr) {
    afold(function(x,y){x+y}, 0, arr)
};

### product of array elements
let aprod := context (afold) in closure(arr) {
    afold(function(x,y){x*y}, 1, arr)
};

### reduce function on arrays
let areduce := function (f,arr) {
    if @len(arr) < 1 {
        ()
    } else {
        let acc := arr(0);
        let i := 1;
        while i < @len(arr) {
            acc := f(acc,arr(i));
            i := i + 1;
        }
        acc
    }
};

### filter function on arrays
let afilter := function (f,arr) {
    let i := 0;
    let out := @array(0,());
    while i < @len(arr) {
        if f(arr(i)) {
            @append(out,arr(i));
        }
        i := i + 1;
    }
    out
};

### for function on arrays
let afor := function (f,a) {
    let i := 0;
    while i < @len(a) {
        f(a(i));
        i := i + 1;
    };
};

