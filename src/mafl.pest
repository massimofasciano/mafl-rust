WHITESPACE = _{ " " | "\t" | NEWLINE }

COMMENT = _{ block_comment | line_comment }
// python-style line comments starting with #
line_comment_start = @{ "#" }
line_comment = @{ line_comment_start ~ (!NEWLINE ~ ANY)* ~ (NEWLINE | EOI) }
// recursive block comments (# ... #)
block_comment_start = @{ "(#" }
block_comment_end = @{ "#)" }
block_comment = @{ 
    block_comment_start ~ 
    (!(block_comment_end|block_comment_start) ~ ANY)* ~ 
    block_comment? ~ 
    (!block_comment_end ~ ANY)* ~ (block_comment_end | EOI) 
}

reserved = _{ 
    "if" | "else" | "while" | "unless" | "do" | 
    "for" ~ !"get" | "forget" |
    "let" | "rec" | "ref" | 
    "in" | "use" | "from" | 
    "fun" | "module" | "cons" | "closed" |
    "dyn" ~ !"mut" | "dynmut" |
    "try" | "catch" | "test" | "expect" |
    "true" | "false" | "nil" |
    "and" | "or" | "not" |
    "return" | "break" | "exit" | "continue" | "throw"
}

identifier_syntax = @{
    (ASCII_ALPHA | "_") ~ 
    (ASCII_ALPHANUMERIC | "_")* 
}
identifier = @{ !reserved ~ identifier_syntax | reserved ~ identifier_syntax }

infix_identifier = ${
    "`" ~ ( builtin | identifier ) ~ "`"
}
builtin = ${
    "@" ~ identifier_syntax
}
integer = @{ 
    "-"? ~ ASCII_DIGIT+ 
}
float = @{
    "-"? ~
    ASCII_DIGIT+ ~ &("."|"e") ~
    ("." ~ ASCII_DIGIT+)? ~
    (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}
nil_literal = @{ "nil" }
boolean = _{ true | false }
true = { "true" }
false = { "false" }

char_literal = ${ "'" ~ character ~ "'"}
character = @{ !"'" ~ ( "\\" ~ ("b" | "f" | "n" | "r" | "t") | ANY ) }

string_literal = ${ "\"" ~ string ~ "\"" } // can't make this silent (in this version)
string = @{ 
    (
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
    )* 
}

array = { "[" ~ (expr ~ ("," ~ expr)*)? ~ "]"}

function_args = { (identifier ~ ("," ~ identifier)*)? }

fun_type = { "fun" | "\\" | "cons" | "module" | "dynmut" | "dyn" }
fun = {
    (#fun_type = fun_type) ~ "("? ~ (#arg = identifier ~ ","?)* ~ ")"? ~ (#body = function_block) 
}

closed = {
    "closed" ~ "("? ~ (#var = identifier ~ ","?)* ~ ")"? ~ (#body = block_syntax) 
}

if = {
    "if" ~ expr ~ if_block ~ ("else" ~ if_block)?
}

unless = {
    "unless" ~ expr ~ if_block
}

while = {
    "while" ~ expr ~ block_syntax
}

do_while = {
    "do" ~ block_syntax ~ "while" ~ "(" ~ expr ~ ")"
}

loop = {
    "loop" ~ block_syntax
}

for = {
    "for" ~ identifier ~ "in" ~ expr ~ block_syntax
}

use = {
    ("from" ~ (#source = expr))? ~ "use" ~ (#var = identifier)* 
}

try_catch = {
    "try" ~ expr ~ "catch" ~ identifier ~ block_syntax
}

identifier_array = { "[" ~ (identifier ~ ("," ~ identifier)*)? ~ "]"}

let_type = { !identifier ~ ("rec" | "ref") }
let = {
    "let" ~ (#let_type = let_type)? ~ (#var = (identifier | identifier_array)) ~ ("=" ~ (#val = expr))?
}

forget = {
    "forget" ~ identifier*
}

field_access = { "." ~ identifier }
array_access = { "[" ~ expr ~ "]" }

assign = {
    identifier ~ (array_access | field_access)* ~ (equal | left_arrow | op_equal) ~ expr
}

return = {
    "return" ~ expr?
}
continue = { "continue" }
break = { 
    "break" ~ expr?
}
throw = {
    "throw" ~ expr?
}
exit = { 
    "exit" ~ expr?
}

catch = {
    "catch" ~ "{" ~ sequence ~ "}"
}

test = {
    "test" ~ expr ~ ("expect" ~ expr)?
}


block = {
    "{" ~ sequence ~ "}"
}

function_block = {
    "{" ~ sequence ~ "}"
}

if_block = {
    "{" ~ sequence ~ "}"
}

block_syntax = {
    "{" ~ sequence ~ "}"
}

apply_args = {
    "(" ~ (expr ~ ("," ~ expr)*)? ~ ")"
}

variable = { identifier | builtin }

op_equal = ${
    (add|sub|mult|div|intdiv|mod|exp) ~ "="
}

question = @{ "?" }
exclam = @{ "!" ~ !"=" }
neg = @{ "-" }
not = @{ "not" }
ref = @{ "&" }
deref = @{ "$" }
add = @{ "+" }
sub = @{ "-" }
mult = @{ "*" ~ !"*" }
div = @{ "/" ~ !"/" }
intdiv = @{ "//" }
mod = @{ "mod" | "%" }
ge = @{ ">=" }
le = @{ "<=" }
gt = @{ ">" ~ !"=" }
lt = @{ "<" ~ !("="|"-") }
eq = @{ "==" }
ne = @{ "!=" | "<>" }
and = @{ "and" }
or = @{ "or" }
exp = @{ "**" | "^"  }
pipe = @{ "|" }
equal = @{ "=" }
left_arrow = @{ "<-" }

expr = _{ expr_infix_pipe }
expr_infix_pipe = { expr_infix_id ~ (pipe ~ expr_infix_id)* }
expr_infix_id = { expr_or ~ (infix_identifier ~ expr_or)* }
expr_or = { expr_and ~ (or ~ expr_and)* }
expr_and = { expr_eq ~ (and ~ expr_eq)* }
expr_eq = { expr_rel ~ ((eq|ne) ~ expr_rel)* }
expr_rel = { expr_add ~ ((ge|le|gt|lt) ~ expr_add)* }
expr_add = { expr_mul ~ ((add|sub) ~ expr_mul)* }
expr_mul = { expr_exp ~ ((mult|div|intdiv|mod) ~ expr_exp)* }
expr_exp = { expr_post ~ (exp ~ expr_post)* }
expr_post = { expr_ref ~ (question|exclam)* }
expr_ref = { ref* ~ expr_apply_or_access }
expr_apply_or_access = { expr_prefix ~ (apply_args | field_access | array_access )* }
expr_prefix = { (not|neg|deref)* ~ expr_base }
expr_base = _{
    "(" ~ expr ~ ")" | nil_literal | 
    array | block | if | 
    while | unless | do_while | loop | for | try_catch |
    fun | assign | use | closed |
    continue | 
    boolean | float | integer | string_literal | char_literal | 
    variable | let | return | break | exit | throw | test | forget
}

// expressions that behave like blocks
expr_block = _{ block | if | while | unless | loop | for | try_catch | closed }

semicolons = _{ ";" ~ semicolons | ";" }
nil_implicit = { "" }
// sequence = expressions separated by semicolon(s)
// expressions that behave like blocks don't need the semicolon
// if trailing semicolon, implicit nil value is added
sequence = _{ sequence_nonempty | sequence_empty }
sequence_nonempty = _{
    expr_block ~ sequence_nonempty | expr ~ semicolons ~ sequence | expr
}
sequence_empty = _{ nil_implicit }

// a program file is a sequence of expressions
file = {
    SOI ~ sequence ~ EOI
}

