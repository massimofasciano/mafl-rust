###
### this is the MFEL embedded standard library
###
### it is visisible through the @std module (ex: @std.array.map)
###

## iterators and iterator functions
module iter () {

    function range(start,end) {
        lambda() {
            if start < end {
                let count := start;
                start := start + 1;
                count            
            }
        }
    }
    function collect(iter) {
        let out := @array(0,nil);
        for e in iter { @append(out,e); }
        out
    }
    function map(f,iter) {
        let out := @array(0,nil);
        for e in iter { @append(out,f(e)); }
        out
    }
    function fold(f,acc,iter) {
        for e in iter { acc := f(acc,e) }
        acc
    }
    function sum(iter) {
        fold(lambda(x,y){x+y}, 0, iter)
    }
    function prod(iter) {
        fold(lambda(x,y){x*y}, 1, iter)
    }
    function filter(f,iter) {
        let out := @array(0,nil);
        for e in iter {
            if f(e) { @append(out,e); }
        }
        out
    }
    function foreach(f,a) {
        for e in a { f(e) }
    }
}

## array functions
module array () {

    function reduce(f,arr) {
        if @len(arr) < 1 { 
            @error("reduce called on empty array")
        } else {
            let acc := arr(0);
            let i := 1;
            while i < @len(arr) {
                acc := f(acc,arr(i));
                i := i + 1;
            }
            acc
        }
    }
    function reverse(arr) {
        # reverses the array in place
        let back := @len(arr)-1;
        let front := 0;
        while back > front {
            let tmp := arr(back);
            arr(back,arr(front));
            arr(front,tmp);
            back := back - 1;
            front := front + 1;
        }
        arr
    }
}

